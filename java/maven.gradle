apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

final String GROUP_ID = 'io.ably'
final String ARTIFACT_ID = 'ably-java'
final String LOCAL_RELEASE_DESTINATION = "${buildDir}/release/${version}"
final String MAVEN_USER = hasProperty('ossrhUsername') ? ossrhUsername : ''
final String MAVEN_PASSWORD = hasProperty('ossrhPassword') ? ossrhPassword : ''

/*
 * Task which signs and uploads the Java artifacts to Nexus OSSRH.
 */
uploadArchives {
    signing {
        sign configurations.archives
    }
    repositories.mavenDeployer {
        logger.lifecycle('OSSRH auth with username: ' + MAVEN_USER)

        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: MAVEN_USER, password: MAVEN_PASSWORD)
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
            authentication(userName: MAVEN_USER, password: MAVEN_PASSWORD)
        }

        pom.groupId = GROUP_ID
        pom.artifactId = ARTIFACT_ID
        pom.version = version

        pom.project {
            name 'Ably java client library'
            description 'A Java Realtime and REST client library for [Ably.io](https://www.ably.io), the realtime messaging service.'
            packaging 'jar'
            inceptionYear '2015'
            url 'https://www.github.com/ably/ably-java'
            scm {
                url 'scm:git:https://github.com/ably/ably-java'
                connection 'scm:git:https://github.com/ably/ably-java'
                developerConnection 'scm:git:git@github.com:ably/ably-java'
            }
            organization {
                name 'Ably'
                url 'http://ably.io'
            }
            issueManagement {
                system 'Github'
                url 'https://github.com/ably/ably-java/issues'
            }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'https://raw.github.com/ably/ably-java/main/LICENSE'
                    distribution 'repo'
                }
            }
        }

        // Exclude test dependencies
        pom.whenConfigured { p ->
            p.dependencies = p.dependencies.findAll {
                dep -> dep.scope == 'runtime'
            }
        }

        // Export files to local storage
        // COMMENT OUT THIS LINE IN ORDER TO RELEASE TO SONATYPE NEXUS STAGING
        // TODO https://github.com/ably/ably-java/issues/566
        repository(url: "file://${LOCAL_RELEASE_DESTINATION}")
    }
}

task zipRelease(type: Zip) {
    from LOCAL_RELEASE_DESTINATION
    destinationDir buildDir
    archiveName "release-${version}.zip"
}

task assembleRelease {
    doLast {
        logger.quiet("Release ${version} can be found at ${LOCAL_RELEASE_DESTINATION}")
        logger.quiet("Release ${version} zipped can be found ${buildDir}/release-${version}.zip")
    }
    dependsOn(uploadArchives)
    dependsOn(zipRelease)
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
