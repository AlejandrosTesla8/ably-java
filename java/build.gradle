plugins {
    id 'checkstyle'
    id 'org.gradle.test-retry' version '1.2.0'
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply from: 'publish.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    api project(':core')

    // Enable access to test classes from the :lib module for integration test cases sharing
    testImplementation project(path: ':core', configuration: 'sharedTestClasses')
}

publish.dependsOn checkstyleMain

task testRealtimeSuite(type: Test) {
    filter {
        includeTestsMatching '*RealtimeSuite'
    }
    beforeTest { descriptor ->
        logger.lifecycle("-> $descriptor")
    }
    outputs.upToDateWhen { false }
    testLogging.exceptionFormat = 'full'
    retry {
        maxRetries = 3
        maxFailures = 4
    }
}

task testRestSuite(type: Test) {
    filter {
        includeTestsMatching '*RestSuite'
    }
    beforeTest { descriptor ->
        logger.lifecycle("-> $descriptor")
    }
    outputs.upToDateWhen { false }
    testLogging.exceptionFormat = 'full'
    retry {
        maxRetries = 3
        maxFailures = 4
    }
}
